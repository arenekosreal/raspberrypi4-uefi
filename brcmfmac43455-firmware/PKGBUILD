# Maintainer: zhanghua <zhanghua.00@qq.com>

pkgname=brcmfmac43455-firmware
pkgver=20210315.3+rpt4
pkgrel=3
pkgdesc="Binary firmware of brcmfmac43455 for Raspberry Pi 4"
url=https://git.kernel.org/cgit/linux/kernel/git/firmware/linux-firmware.git
arch=("any")
license=("custom")
conflicts=("firmware-raspberrypi")
provides=("firmware-raspberrypi")
source=(
    firmware_bcm80211.deb::http://archive.raspberrypi.org/debian/pool/main/f/firmware-nonfree/firmware-brcm80211_${pkgver/./-}_all.deb
    https://github.com/RPi-Distro/bluez-firmware/raw/master/broadcom/BCM4345C0.hcd
    )
sha256sums=('c98c7160505d4c1f22dcfbda5ae4faf7c2779ab1a364d71a674a8bb2a5faf878'
            'c723c903655086c9f3a1de94a291f495a0f087f1ee6d3ee7bc4c558497aae7bc')
options=("!strip")

package(){
    cd ${srcdir}
    tar xf data.tar.xz ./lib/firmware
    tar xf data.tar.xz ./usr/share/doc/firmware-brcm80211/copyright
    for file in brcmfmac43455-sdio.bin brcmfmac43455-sdio.clm_blob brcmfmac43455-sdio.txt brcmfmac43455-sdio.raspberrypi,4-model-b.txt
    do
        install -Dm644 ${srcdir}/lib/firmware/brcm/${file} ${pkgdir}/usr/lib/firmware/updates/brcm/${file}
    done
    install -Dm644 ${srcdir}/BCM4345C0.hcd ${pkgdir}/usr/lib/firmware/updates/brcm/BCM4345C0.hcd
    ln -s brcmfmac43455-sdio.raspberrypi,4-model-b.txt "${pkgdir}/usr/lib/firmware/updates/brcm/brcmfmac43455-sdio.Raspberry Pi Foundation-Raspberry Pi 4 Model B.txt"
    ln -s BCM4345C0.hcd ${pkgdir}/usr/lib/firmware/updates/brcm/BCM.hcd
    ln -s brcmfmac43455-sdio.bin "${pkgdir}/usr/lib/firmware/updates/brcm/brcmfmac43455-sdio.Raspberry Pi Foundation-Raspberry Pi 4 Model B.bin"
    install -Dm644 ${srcdir}/usr/share/doc/firmware-brcm80211/copyright ${pkgdir}/usr/share/licenses/${pkgname}/LICENCE
}
